plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'ohi.andre.consolelauncher'
    compileSdk = libs.versions.compileSdk.get().toInteger()
    
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    
    defaultConfig {
        applicationId "ohi.andre.consolelauncher"
        minSdk = libs.versions.minSdk.get().toInteger()
        targetSdk = libs.versions.targetSdk.get().toInteger()
        versionName "v6.16"
        versionCode 206
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
            minifyEnabled false
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    lint {
        checkReleaseBuilds false
    }
}

dependencies {
    api libs.androidx.appcompat
    implementation libs.androidx.localbroadcastmanager
    implementation libs.kotlin.stdlib
    implementation libs.okhttp
    implementation libs.htmlcleaner
    implementation libs.jsonpath
    implementation libs.jsonsmart
    implementation libs.jsoup
    implementation libs.comparestring
}
